buildscript {
    repositories {
        maven { url = "http://maven.nationsglory.fr/releases/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-NGWascar-rev3'
    }
}
apply plugin: 'forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.6.4-1.20"
group = "net.montoyo.mcef"
archivesBaseName = "mcef"
def enableSigning = false

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.6.4-9.11.1.964"
     
}

//MCEF: update Let's Encrypt root certificate before building
/*task importLetsEncryptCertificate {
    def target = new File(sourceSets.main.resources.srcDirs.first(), "assets/mcef/r3.crt")
    if(target.exists())
        target.delete()

    new URL("https://montoyo.net/ShareX/root.crt").withInputStream { i -> target.withOutputStream { it << i }}
    println "updated let's encrypt root certificate"
}*/

//processResources.dependsOn importLetsEncryptCertificate

//MCEF: Generate unobfuscated JAR that can be added to the 'libs' folder
task apiJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'api'
	
	manifest {
        attributes 'FMLCorePlugin': 'net.montoyo.mcef.coremod.ShutdownPatcher'
        attributes 'FMLCorePluginContainsMod': 'true'
    }
}

//Generate coremod JAR
import groovy.swing.SwingBuilder

task coremodJar(type: Jar) {
    from(sourceSets.main.output) {
        include 'net/montoyo/mcef/coremod/**'
    }

    manifest {
        attributes 'FMLCorePlugin': 'net.montoyo.mcef.coremod.ShutdownPatcher'
    }

    classifier = 'coremod'

    doLast {
        if(enableSigning) {
            def keyAlias = ''
            def keyFile = ''
            def keyPswd = ''
            def dstDir = new File(coremodJar.destinationDir, 'signed')
            dstDir.mkdirs()

            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Signing details required',
                        alwaysOnTop: true,
                        resizable: false,
                        locationRelativeTo: null,
                        pack: true,
                        show: true) {
                    vbox {
                        label(text: 'Key File:')
                        input1 = textField()
                        label(text: 'Key Alias:')
                        input2 = textField()
                        label(text: 'Key Password:')
                        input3 = passwordField()
                        button(defaultButton: true, text: 'Sign', actionPerformed: {
                            keyFile = input1.text
                            keyAlias = input2.text
                            keyPswd = new String(input3.password)
                            dispose()
                        })
                    }
                }
            }

            ant.signjar(
                    jar: coremodJar.archivePath.absolutePath,
                    destDir: dstDir.absolutePath,
                    alias: keyAlias,
                    storetype: 'jks',
                    keystore: keyFile,
                    storepass: keyPswd,
                    verbose: true,
                    preservelastmodified: 'true'
            )
        }
    }
}

jar {
    exclude 'net/montoyo/mcef/coremod/**'

    File signedCoremod
    if(enableSigning)
        signedCoremod = new File(new File(coremodJar.destinationDir, 'signed'), coremodJar.archivePath.name)
    else
        signedCoremod = coremodJar.archivePath

    from(signedCoremod.absolutePath) {
        include '*'
    }

    manifest {
        attributes 'ContainedDeps': coremodJar.archivePath.name
        attributes 'Main-Class': 'net.montoyo.mcef.setup.SetupUI'
    } 
}

artifacts {
    archives apiJar
}

dependencies {
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
